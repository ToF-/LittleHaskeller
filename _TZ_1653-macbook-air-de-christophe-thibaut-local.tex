\newpage
%----------------------------------------------------------------------------------------------------
\section{``Pair'' Programming} 
%----------------------------------------------------------------------------------------------------
\vspace{10cm}
\hrule

\lhQ Now that we have suitable tools to compare cards, what should we do?
\lhA Compare hands.
\lhN How do we form a \il!Hand!?
\lhA We'll write a function:
\begin{lstlisting}[frame=single]
type Hand = [Card]
hand :: String -> Hand
\end{lstlisting}
\lhN Good. But we should write a test before writing code.
\lhA Go on.
\lhN What is the simplest hand comparison we could write a test for?
\lhA Let's try comparing simple ``High Cards'' hands.
\lhN Ok. Here is a new test:
\begin{lstlisting}[frame=single,escapechar=!]
main = runTestTT $ TestList 
       [sortBy (comparing card) ud  ~?= sd
       ,map suit (cards "A♣ A♦ A♥ A♠") ~?= ['♣','♦','♥','♠']
       ,flush (cards "A♣ T♣ 3♣ 4♣ 2♣") ~?= True
       ,flush (cards "A♠ T♣ 3♣ 4♣ 2♣") ~?= False
       ,flush (cards "A♠ T♠ 3♠ 4♠ 2♠") ~?= True
       ,comparing hand "6♣ 4♦ A♣ 3♠ K♠" "8♠ J♥ 7♦ 5♥ 6♣" ~?= GT]
    where cards = map card . words 
\end{lstlisting} % $
\hspace*{\fill}
\lhA This last test is a bit long.
\lhN Ok, let's rephrase it this way:
\begin{lstlisting}[frame=single]
main = runTestTT $ TestList 
       [sortBy (comparing card) ud  ~?= sd
       ,map suit (cards "A♣ A♦ A♥ A♠") ~?= ['♣','♦','♥','♠']
       ,flush (cards "A♣ T♣ 3♣ 4♣ 2♣") ~?= True
       ,flush (cards "A♠ T♣ 3♣ 4♣ 2♣") ~?= False
       ,flush (cards "A♠ T♠ 3♠ 4♠ 2♠") ~?= True
       ,"6♣ 4♦ A♣ 3♠ K♠" `beat` "8♥ J♥ 7♦ 5♥ 6♣"]
    where cards = map card . words 
          beat h g = comparing hand h g ~?= GT
\end{lstlisting} % $
\hspace*{\fill}
\lhA \error OK. We need to create the \il!hand! function. But first I will borrow your \il!cards! utility function.
\lhN Sure, take it to your side.
\begin{lstlisting}[frame=single]
main = runTestTT $ TestList 
       [sortBy (comparing card) ud  ~?= sd
       ,map suit (cards "A♣ A♦ A♥ A♠") ~?= ['♣','♦','♥','♠']
       ,flush (cards "A♣ T♣ 3♣ 4♣ 2♣") ~?= True
       ,flush (cards "A♠ T♣ 3♣ 4♣ 2♣") ~?= False
       ,flush (cards "A♠ T♠ 3♠ 4♠ 2♠") ~?= True
       ,"6♣ 4♦ A♣ 3♠ K♠" `beat` "8♥ J♥ 7♦ 5♥ 6♣"]
    where beat h g = comparing hand h g ~?= GT
\end{lstlisting} % $
\hspace*{\fill}
\lhA \error Thanks
\begin{lstlisting}[frame=single]
cards :: String -> [Cards]
cards = map card . words 
\end{lstlisting}
In fact forming a hand is just making \il!Card!s from \il!String!s and sorting them:
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand = sort . cards
\end{lstlisting}
\failure Except we get \il!LT! instead of \il!GT!.
\lhN Of course: we're sorting in the wrong order. How can we change the sorting order?
\lhA We can use \il!sortBy! and a give it the proper comparison function. 
\lhN Given what $GHCI$ tells us about \il!sort!, \il!sortBy! and \il!compare!:
\begin{small}
\begin{verbatim}
:type sort
sort :: (Ord a) => [a] -> [a]
:type sortBy
sortBy :: (a -> a -> Ordering) -> [a] -> [a]
:type compare
compare :: (Ord a) => a -> a -> Ordering
\end{verbatim}
\end{small}
We know that \il!sortBy compare! is equivalent to \il!sort!. 
How can we reverse the result given by \il!compare!?
\lhA By \il!flip!ping its arguments. \il!flip f a b! is equivalent to \il!f b a!. Thus:
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand = sortBy (flip compare) . cards
\end{lstlisting}
\success will do the trick.
\lhN Ok. What is the next hand that can beat a \emph{High Card}?
\lhA A \emph{Pair}.
\lhN Then I'll write this test:
\begin{lstlisting}[frame=single]
       ,"5♥ 2♦ 4♦ 3♥ 2♥" `beat` "A♥ K♥ Q♦ J♦ 9♥"]
    where beat h g = comparing hand h g ~?= GT
\end{lstlisting}
Meaning: the lowest \emph{Pair} should beat the highest \emph{High Card}.
\lhA \failure The test fails. We have to detect that the hand is a pair, and use that information to trump the usual card comparison.
\lhN How do we do that?
\lhA \failure We declare that, within the \il!Hand! type, a \emph{Pair} is always greater than a \emph{High Card}. 
\lhN How do we order values within a type?
\lhA \failure We declare it as an algebraic type, saying we either have a \il!HighCard! followed by a list of \il!Card!s, or a \il!Pair!:
\begin{lstlisting}[frame=single]
data Hand = HighCard [Card]
            | Pair
            deriving (Ord,Eq)
\end{lstlisting}
\error Of course, now the implementation of \il!hand! doesn't yield a correct \il!Hand! value.
\lhN The compiler says:
\begin{small}
\begin{verbatim}
Couldn't match expected type `Hand' 
    against inferred type `[Card]'
In the expression: 
   sortBy (flip compare) . cards
In the definition of `hand': 
   hand = sortBy (flip compare) . cards
\end{verbatim}
\end{small}
Can you arrange this?
\lhA Yes. Let's begin by forcing the function to a \il!HighCard! value:
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand = HighCard . sortBy (flip compare) . cards
\end{lstlisting}
\failure and we're back with a failing test instead of a compiler error.
\lhN Can you \emph{fake} the correct construction that would make the test pass?
\lhA Yes. Let's just insert a special case:
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand "5♥ 2♦ 4♦ 3♥ 2♥" = Pair 
hand s = HighCard $ sortBy (flip compare) $ cards s
\end{lstlisting}
\success And the test is passing, because given the declaration of \il!Hand!, \il!Pair! is a higher \il!Hand! value thant \il!HighCard!.
\lhN Now we need to triangulate, so I'm adding a new test about a \emph{Pair} beating a \emph{High Card}:
\begin{lstlisting}[frame=single]
       ,"5♥ 2♦ 3♥ 4♦ 2♥" `beat` "A♥ K♥ Q♦ J♦ T♥"
       ,"5♥ 4♦ 3♥ 2♦ 3♣" `beat` "A♥ K♥ Q♦ J♦ T♥"]
\end{lstlisting}
\hspace*{\fill}
\lhA \failure I'll aggravate my \emph{fake} with a new pattern:
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand "5♥ 2♦ 3♥ 4♦ 2♥" = Pair 
hand "5♥ 4♦ 3♥ 2♦ 3♣" = Pair
hand s = HighCard $ sortBy (flip compare) $ cards s
\end{lstlisting}
\success And now we have to think.
\lhN How can we get rid of these \emph{fake} implementations?
\lhA By writing a function from \il!String! to \il!Bool! that detects a \emph{Pair}.
\lhN If you had this function, what would the \il!hand! function look like?
\lhA It would look like this:
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand s | hasAPair s = Pair
hand s = HighCard $ sortBy (flip compare) $ cards s
\end{lstlisting}
\error The code is broken, now.
\lhN Can you write the function \il!hasAPair!?
\lhA Yes:
\begin{lstlisting}[frame=single]
hasAPair :: String -> Bool
hasAPair "5♥ 2♦ 3♥ 4♦ 2♥" = True 
hasAPair "5♥ 4♦ 3♥ 2♦ 3♣" = True
hasAPair _ = False
\end{lstlisting}
\success Done.
\lhN There's a bit of noise in these patterns. Do we really need to deal with \il!String!s?
\lhA \success No, we can match patterns on the card \il!Value!s:
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand s | hasAPair $ map value $ cards s = Pair
hand s = HighCard $ sortBy (flip compare) $ cards s

hasAPair :: [Value] -> Bool
hasAPair [5,2,3,4,2] = True 
hasAPair [5,4,3,2,3] = True
hasAPair _ = False
\end{lstlisting}
\lhN Would it help if we sorted the values?
\lhA \success That would clarify the patterns, so let's do it:
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand s | hasAPair $ sort $ map value $ cards s = Pair
hand s = HighCard $ sortBy (flip compare) $ cards s

hasAPair :: [Value] -> Bool
hasAPair [2,2,3,4,5] = True 
hasAPair [2,3,3,4,5] = True
hasAPair _ = False
\end{lstlisting} % $ 
\lhN Do you see something common between the first two patterns of \il!hasAPair!?
\lhA Apart from the fact they both end with \il!3,4,5]!, no.
\lhN Can you group the values after sorting them ?
\lhA \success Ok. We have to change the signature for the function.
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand s | hasAPair $ group $ sort $ map value $ cards s = Pair
hand s = HighCard $ sortBy (flip compare) $ cards s

hasAPair :: [[Value]] -> Bool
hasAPair [[2,2],[3],[4],[5]] = True 
hasAPair [[2],[3,3],[4],[5]] = True
hasAPair _ = False
\end{lstlisting}
\success Oh. Now I see something.
\lhN What do you see?
\lhA Each list contains four groups. So that would be a way to detect any \emph{Pair}!
\lhN How would write the function, then ?
\lhA \success Like this:
\begin{lstlisting}[frame=single]
hasAPair :: [[Value]] -> Bool
hasAPair gs = length gs == 4 
\end{lstlisting}
\success The code is still quite messy, though.
\lhN How can we refactor ?
\lhA First, factorize parts of the expression, like \il!cards s!   
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand s | hasAPair $ group $ sort $ map value $ cs = Pair
hand s = HighCard $ sortBy (flip compare) $ cs
         where cs = cards s
\end{lstlisting}   
\failure Oops. That doesn't work
\lhN The compiler says:
\begin{small}
\begin{verbatim}
Not in scope: `cs'
\end{verbatim}
\end{small}
Your \il!cs! variable should be declared for the first pattern too.
\lhA Ok. Let's go back to green.
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand s | hasAPair $ group $ sort $ map value $ cs = Pair
       where cs = cards s
hand s = HighCard $ sortBy (flip compare) $ cs
         where cs = cards s
\end{lstlisting}
\success Now we can continue to refactor.
\lhN How can you write only one pattern in this function ?
\lhA \success By using an \il!if!:
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand s = if hasAPair $ group $ sort $ map value $ cs then Pair
         else HighCard $ sortBy (flip compare) $ cs
       where cs = cards s
\end{lstlisting}
\lhN Now, add legibility.
\lhA \success Let's have more auxiliary functions, and bring \il!hasAPair! where it belongs:
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand s = if hasAPair (groups cs) then Pair
         else HighCard $ sortBy (flip compare) $ cs
       where cs = cards s
             groups = group . sort . map value
             hasAPair gs = length gs == 4 
\end{lstlisting}
\lhN In this function, we sort the cards twice. Would the \il!group!ing still work if it used \il!sortBy (flip compare)! instead of \il!sort! ?
\lhA \success Let's ask the code:
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand s = if hasAPair (groups cs) then Pair
         else HighCard $ sortBy (flip compare) $ cs
       where cs = cards s
             groups = group . sortBy (flip compare) . map value
             hasAPair gs = length gs == 4 
\end{lstlisting}
\success Yes, the criteria of having four groups still holds, whatever the order in which sort the cards.
\lhN So we can factorize the sorting.
\lhA \success Right. Now \il!cs! represent the sorted cards:
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand s = if hasAPair (groups cs) then Pair
         else HighCard cs
       where cs = sortBy (flip compare) $ cards s
             groups = group . map value
             hasAPair gs = length gs == 4 
\end{lstlisting} % $ 
\success But, this code is still too long.
\lhN Maybe we can get rid of \il!hasAPair!
\lhA \success Let's try:
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand s = case length $ groups cs of
           4 -> Pair
           5 -> HighCard cs 
       where cs = sortBy (flip compare) $ cards s
             groups = group . map value
\end{lstlisting}
\success Right.
\lhN And harmonize variable names, like \il!gs! instead of \il!groups!...
\lhA \success You mean like this:
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand s = case length gs  of
           4 -> Pair
           5 -> HighCard cs 
       where cs = sortBy (flip compare) $ cards s
             gs = group $ map value $ cs
\end{lstlisting} % $
\success Yeah, that's a bit clearer.
\lhN Can you add symmetry ? Using \il!groupBy! instead of \il!group! and \il!map!.
\lhA \success Sure:
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand s = case length gs  of
           4 -> Pair
           5 -> HighCard cs 
       where cs = sortBy (flip compare) $ cards s
             gs = groupBy (same value) cs
             same f a b = f a == f b
\end{lstlisting} % $
\success That's even clearer.
\lhN Hey, that \il!same! function is interesting. Do you see where we met a case for it before ?
\lhA No.
\lhN Look at the \il!flush! function.
\lhA Here it is:
\begin{lstlisting}[frame=single]
flush :: [Card] -> Bool
flush (c:cs) = all (\x -> suit x == suit c) cs
\end{lstlisting}
\lhN Can you use something similar to the function \il!same! here?
\lhA Let's try:
\begin{lstlisting}[frame=single]
same :: (Eq a) => (t -> a) -> t -> t -> Bool
same f a b = f a == f b

flush :: [Card] -> Bool
flush (c:cs) = all (\x -> same suit c x) cs

\end{lstlisting}
\success You are right.
\lhN Simplify, then!
\lhA Ok. \begin{lstlisting}[frame=single]
flush :: [Card] -> Bool
flush (c:cs) = all (same suit c) cs

hand :: String -> Hand
hand s = case length gs  of
           4 -> Pair
           5 -> HighCard cs 
       where cs = sortBy (flip compare) $ cards s
             gs = groupBy (same value) cs
\end{lstlisting} % $
\lhN Ok. Here's is the test code:
\begin{lstlisting}[frame=single]
module Tests
where 
import Test.HUnit
import PokerHand
import Data.Ord (comparing)
import Data.List (sort,sortBy)

ud = words "A♣ 2♣ T♣ K♣ 9♣ Q♣ J♣"
sd = words "2♣ 9♣ T♣ J♣ Q♣ K♣ A♣"

main = runTestTT $ TestList 
       [sortBy (comparing card) ud  ~?= sd
       ,map suit (cards "A♣ A♦ A♥ A♠") ~?= ['♣','♦','♥','♠']
       ,flush (cards "A♣ T♣ 3♣ 4♣ 2♣") ~?= True
       ,flush (cards "A♠ T♣ 3♣ 4♣ 2♣") ~?= False
       ,flush (cards "A♠ T♠ 3♠ 4♠ 2♠") ~?= True
       ,"6♣ 4♦ A♣ 3♠ K♠" `beat` "8♥ J♥ 7♦ 5♥ 6♣"
       ,"5♥ 2♦ 3♥ 4♦ 2♥" `beat` "A♥ K♥ Q♦ J♦ T♥"
       ,"5♥ 4♦ 3♥ 2♦ 3♣" `beat` "A♥ K♥ Q♦ J♦ T♥"]
    where beat h g = comparing hand h g ~?= GT
\end{lstlisting} % $
\hspace*{\fill}
\lhA \success And this is the tested code: first the type declarations:
\begin{lstlisting}[frame=single]
module PokerHand
where
import Char
import Data.List

data Card = C { value :: Value, suit :: Suit } 
     deriving (Ord,Eq)
type Value = Int
type Suit = Char

data Hand = HighCard [Card] | Pair  deriving (Ord,Eq)
\end{lstlisting}
\lhN 
\hspace*{\fill}
\lhA And the functions:

card :: String -> Card
card [v,s] = C (toValue v) s
    where 
      toValue 'A' = 14
      toValue 'K' = 13
      toValue 'Q' = 12
      toValue 'J' = 11
      toValue 'T' = 10
      toValue  c  = ((ord c) - (ord '0'))

flush :: [Card] -> Bool
flush (c:cs) = all (same suit c) cs

same :: (Eq a) => (t -> a) -> t -> t -> Bool
same f a b = f a == f b

hand :: String -> Hand
hand s = case length gs  of
           4 -> Pair
           5 -> HighCard cs 
       where cs = sortBy (flip compare) $ cards s
             gs = groupBy (same value) cs

cards :: String -> [Card]
cards = map card . words 
\end{lstlisting}
\lhN What should we do now ?
\lhA Have some rest!
\lhend



\bye
