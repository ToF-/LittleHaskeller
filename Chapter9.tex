\newpage
%----------------------------------------------------------------------------------------------------
\section{Solving the problem} 
%----------------------------------------------------------------------------------------------------
\vspace{10cm}
\hrule

\lhQ Did we solve our problem?
\lhA Not yet. 
\lhN What do we need to do, then?
\lhA Mark the lines from the input with the ranking of the hand, and suffix the bast one with \il!"(winner")!.
\lhN What test should I write?
\lhA Write the simplest test you can think of.
\lhN What is the trivial case for a function that should mark lines?
\lhA No hand at all.
\lhN Ok
\begin{lstlisting}[frame=single]
markResults [Nothing] ~?= [""]
\end{lstlisting}
If we don't have a hand, then there is no mark.
\lhA \error I see. Here's the function:
\begin{lstlisting}[frame=single]
markResults :: [Maybe Ranking] -> [String]
markResults _ = [""]
\end{lstlisting}
\success Done.
\lhN Here's my next case. 
\begin{lstlisting}[frame=single]
markResults [Nothing, Just Pair] 
               ~?= ["","Pair (winner)"]
\end{lstlisting}
\lhA \failure Ok. I'll just add a pattern:
\begin{lstlisting}[frame=single]
markResults :: [Maybe Ranking] -> [String]
markResults [Nothing]   = [""]
markResults [Nothing, Just Pair] = ["", "Pair (winner)"]
\end{lstlisting}
\success It's a \emph{fake}, as usual.
\lhN Do you see a possible refactoring here?
\lhA I see a \il!map!:
\begin{lstlisting}[frame=single]
markResults :: [Maybe Ranking] -> [String]
markResults = map mark
    where mark Nothing = ""
          mark (Just Pair) = "Pair (winner)"
\end{lstlisting}
\success Refactoring done.
\lhN Here's a new case:
\begin{lstlisting}[frame=single]
markResults [Nothing, Just Pair, Just HighCard] ~?= 
                ["","Pair (winner)","High Card"]
\end{lstlisting}
\error We can have several hands. The best one is the winner.
There's \emph{non-exaustive patterns} error in our code, now.
\lhA \error Sure. Here's a fix:
\begin{lstlisting}[frame=single]
markResults :: [Maybe Ranking] -> [String]
markResults = map mark
    where mark Nothing = ""
          mark (Just Pair) = "Pair (winner)"
          mark (Just r) = show r
\end{lstlisting}
\success It's still a \emph{fake}.
\lhN How can we remove the \emph{fake}?
\lhA By comparing each value in the list with the maximum value in the list.
\begin{lstlisting}[frame=single]
markResults :: [Maybe Ranking] -> [String]
markResults rs = map mark rs
    where mark Nothing = ""
          mark v@(Just r) 
              | v == maximum rs = show r ++ " (winner)" 
          mark (Just r) = show r 
\end{lstlisting}
\success It works!
\lhN Can you remove duplication?
\lhA Yes.
\begin{lstlisting}[frame=single]
markResults :: [Maybe Ranking] -> [String]
markResults rs = map mark rs
    where mark Nothing = ""
          mark v@(Just r) = (show r) ++ if (v == maximum rs) then  " (winner)" else ""
\end{lstlisting}
\success Done.
\lhN Could we have pattern in lieu of the \il!if then else!?
\lhA Yes.
\begin{lstlisting}[frame=single]
markResults :: [Maybe Ranking] -> [String]
markResults rs = map mark rs
    where mark Nothing = ""
          mark (Just r) = (show r) ++ winner (Just r)
          winner v | v == maximum rs = " (winner)"
          winner _ = ""
\end{lstlisting}
\success Done.
\lhN And we could avoid computing the \il!maximum! at each line.
\lhA You are right.
\begin{lstlisting}[frame=single]
markResults :: [Maybe Ranking] -> [String]
markResults rs = map mark rs
    where mark Nothing = ""
          mark (Just r) = (show r) ++ winner (Just r)
          winner v | v == m = " (winner)"
          winner _ = ""
          m = maximum rs
\end{lstlisting}
\lhend
