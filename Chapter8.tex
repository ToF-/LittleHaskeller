\newpage
%----------------------------------------------------------------------------------------------------
\section{Finding Hands} 
%----------------------------------------------------------------------------------------------------
\vspace{10cm}
\hrule

\lhQ We know how to compute a hand's ranking, and print that ranking. What do we need to do now?
\lhA We need to find the five card hand with the best ranking in a list of cards.
\lhN How do we do that?
\lhA Just write a failing test.
\lhN Ok. Here we go:
\begin{lstlisting}[frame=single]
maxRanking "6♥ 6♦ 6♠ 6♣ K♠ K♦" ~?= Nothing
\end{lstlisting}
In that case, the result is \il!Nothing! because there are less than 7 cards in the string. You know about \il!Nothing!, right ?
\lhA \error Yes. 
\begin{lstlisting}[frame=single]
maxRanking :: String -> Maybe Ranking
maxRanking _ = Nothing
\end{lstlisting}
\success Your test is implying that \il!maxRanking! consumes a \il!String! and returns, \il!Maybe!, a \il!Ranking!. 
\lhN That is correct. Here's another one:
\begin{lstlisting}[frame=single]
maxRanking "6♣ 4♦ A♣ 3♠ K♠ T♦ 8♣" ~?= Just HighCard
\end{lstlisting}

\lhA \failure I'll make it pass as fast as I can:
\begin{lstlisting}[frame=single]
maxRanking :: String -> Maybe Ranking
maxRanking s | length (cards s) < 7 = Nothing
maxRanking _ = Just HighCard
\end{lstlisting}
\success We just ignore lists of less than 7 cards.
\lhN Ok. But there is still a \emph{fake}. Here's a new test:
\begin{lstlisting}[frame=single]
maxRanking "6♣ 6♦ A♣ 3♠ K♠ T♥ 8♦" ~?= Just Pair
\end{lstlisting}
\lhA Easy: we just return the ranking of the list of cards
\begin{lstlisting}[frame=single]
maxRanking :: String -> Maybe Ranking
maxRanking s | length (cards s) < 7 = Nothing
maxRanking s = Just $ ranking $ hand s
\end{lstlisting}
\error Uh oh.
\lhN
\begin{small}
\begin{verbatim}
Not in scope: ranking
\end{verbatim}
\end{small}
We don't have a function \il!ranking!. We had one, but we renamed it.
\lhA Ok, here's the needed function:
\begin{lstlisting}[frame=single]
ranking :: Hand -> Ranking
ranking (H r _) = r
\end{lstlisting}
\error But the test still doesn't pass.
\lhN 
\begin{small}
\begin{verbatim}
Non-exhaustive patterns in function calcRank
\end{verbatim}
\end{small}
We have several messages like this. 
\lhA Of course, that function recognizes only five card patterns:
\begin{lstlisting}[frame=single]
rank :: [[Card]] -> Hand
rank gs = H (calcRank gs) (concat gs)  
    where calcRank [[_,_,_,_],_]   = FourOfAKind 
          calcRank [[_,_,_],_]     = FullHouse
          calcRank [[_,_,_],_,_]   = ThreeOfAKind
          calcRank [[_,_],[_,_],_] = TwoPairs
          calcRank [[_,_],_,_,_]   = Pair    
          calcRank [_,_,_,_,_]     = HighCard 
\end{lstlisting}
\error and I fed it with a seven card list.
\lhN What should we do then?
\lhA To make this test pass, let's cut the list and take only five cards. This means taking only the first 14 chars of the string:
\begin{lstlisting}[frame=single]
maxRanking :: String -> Maybe Ranking
maxRanking s | length (cards s) < 7 = Nothing
maxRanking s = Just $ ranking $ hand $ take 14 s
\end{lstlisting} % $
\success See? It works.
\lhN That's not very clear. Couldn't we just say:\\ \il!hand $ take 5 $ cards s! \\ instead?  
\lhA We can always write it:
\begin{lstlisting}[frame=single]
maxRanking :: String -> Maybe Ranking
maxRanking s | length (cards s) < 7 = Nothing
maxRanking s = Just $ ranking $ hand $ take 5 $ cards s
\end{lstlisting}
\error But it doesn't work because \il!hand! consumes \il!String!s, not lists of \il!Card!s.
\lhN Then make it accept a list of \il!Card!s.
\lhA \error Ok. Heres the \il!hand! function:
\begin{lstlisting}[frame=single]
hand :: String -> Hand
hand =     cards
       >>. rSortBy (comparing value)
       >>. groupBy (same value)
       >>. rSortBy (comparing length)
       >>. rank
       >>. promoteStraight
       >>. promoteFlush    
\end{lstlisting}
\error So, I change the signature, and get rid of the first call to \il!cards!, this call will be made somewhere above.:
\begin{lstlisting}[frame=single]
hand :: [Card] -> Hand
hand =     rSortBy (comparing value)
       >>. groupBy (same value)
       >>. rSortBy (comparing length)
       >>. rank
       >>. promoteStraight
       >>. promoteFlush    
\end{lstlisting}         
\error We still have an error.
\lhN
\begin{small}
\begin{verbatim} 
Couldn't match expected type `Card' against 
inferred type `Char'
\end{verbatim}
\end{small}
It's in the tests. I need to change this line:
\begin{lstlisting}[frame=single]
beat h g = comparing hand  h g ~?= GT
\end{lstlisting}
like this:
\begin{lstlisting}[frame=single]
beat h g = comparing (hand . cards) h g ~?= GT
\end{lstlisting}
\success And everything is back to normal.
\lhA Good.
\lhN We should also change the signature of our function \il!maxRanking! so that it also receive a \il![Card]!.
I will change my test to that effect:
\begin{lstlisting}[frame=single]
maxRank "6♥ 6♦ 6♠ 6♣ K♠ K♦" ~?= Nothing,
maxRank "6♣ 4♦ A♣ 3♠ K♠ T♦ 8♣" ~?= Just HighCard,
maxRank  "6♣ 6♦ A♣ 3♠ K♠ T♥ 8♦" ~?= Just Pair]
where beat h g = comparing (hand . cards) h g ~?= GT
          maxRank = maxRanking . cards 
\end{lstlisting}
\lhA \error I now change the code of the function:
\begin{lstlisting}[frame=single]
maxRanking :: [Card] -> Maybe Ranking
maxRanking cs | length cs < 7 = Nothing
maxRanking cs = Just $ ranking $ hand $ take 5 $ cs
\end{lstlisting}
\success And the refactoring is done. Now we need to replace our \emph{fake} with the general implementation.
\lhN There are several possible 5 card hands we can form with a list of 7 cards. Do you know how much?
\lhA Yes, ${{7}\choose{5}} = \frac{7\times6 \times 5 \times 4 \times 3}{5 \times 4 \times 3 \times 2 \times 1} = \frac{2520}{120} = 21$
\lhN Do you know how to find them?
\lhA Yes. I can use the \il!subsequences! function. For example\\ 
\il!subsequences "CAT"! \\
gives: \\
\il!["","C","A","CA","T","CT","AT","CAT"]!
\lhN What if we want only two letters subsequences ?
\lhA I suppose applying \il!filter ((2==) . lenght)! on the list would do the trick.
\lhN Then we have to find the best hand.
\lhA Oh, that's the simplest part.
\lhN Let's begin with that part, then.
\lhA Allright. Suppose we have computed some sublists already:
\begin{lstlisting}[frame=single]
maxRanking :: [Card] -> Maybe Ranking
maxRanking cs | length cs < 7 = Nothing
maxRanking cs = 
    let sl = [drop 2 cs, take 5 (drop 1 cs), take 5 cs]
    in Just $ ranking $ hand $ take 5 $ cs
\end{lstlisting}
\success Here we don't compute all the 21 sublists, we create 3 of them. 
\lhN Given our last test:
\begin{lstlisting}[frame=single]
maxRank  "6♣ 6♦ A♣ 3♠ K♠ T♥ 8♦" ~?= Just Pair
\end{lstlisting}
the 3 sublists would give these hands: \\\clubs{A} \spades3 \spades{K} \hearts{T} \diamonds8 \\ 
 \diamonds6 \clubs{A} \spades3 \spades{K} \hearts{T} \\
\clubs6 \diamonds6 \clubs{A} \spades3 \spades{K}. \\
Only the last hand would rank as a \emph{Pair}.
\lhA That's right.
\lhN Go on.
\lhA Then, to find the best ranking hand from this list is easy:
\begin{lstlisting}[frame=single]
maxRanking :: [Card] -> Maybe Ranking
maxRanking cs | length cs < 7 = Nothing
maxRanking cs = 
    let sl = [drop 2 cs, take 5 (drop 1 cs), take 5 cs]
        max  = maximum . map ranking . map hand
    in Just $ max sl
\end{lstlisting} %$
\success And we're done for that part.
\lhN Ok. Now how do we compute the sublists?
\lhA Hmm. First we need a helper function to create 5 item sublists from a list
\lhN Ok. What about a function such as:
\begin{lstlisting}[frame=single]
subLists 2 "CAT" ~?= ["CA","CT","AT"]
\end{lstlisting}
I introduce a variable, because I don't really want to write the case for 21 sublists.
\lhA \error Nice idea. Here's the function:
\begin{lstlisting}[frame=single]
subLists :: Int -> [a] -> [[a]]
subLists n = filter ((n ==) . length) . subsequences 
\end{lstlisting}
\success It's a bit more general than needed, thought.
\lhN Ok. Now use the function to find hands.
\lhA Allright:
\begin{lstlisting}[frame=single]
maxRanking :: [Card] -> Maybe Ranking
maxRanking cs | length cs < 7 = Nothing
maxRanking cs = 
    let sl = subLists 5 cs
        max  = maximum . map ranking . map hand
    in Just $ max sl
\end{lstlisting} %$
\success It works!
\lhN Yes. Can you clean up the code? I will delete the test about \il!subLists!, as I think this function is just a helper. 
\lhA Right:
\begin{lstlisting}[frame=single]
maxRanking :: [Card] -> Maybe Ranking
maxRanking cs | length cs < 7 = Nothing
maxRanking cs = Just $ max (subLists cs)
    where 
      max = maximum . map (ranking . hand)
      subLists = filter ((5==).length) . subsequences
\end{lstlisting} %$
\success And we're done.

\lhend 
