\newpage
%----------------------------------------------------------------------------------------------------
\section{Dealing with Cards} % 11111111111111111111111111111111111111111111111111111111111111111111
%----------------------------------------------------------------------------------------------------
\vspace{10cm}
\hrule

\lhQ What is something simple we could begin to solve ?
\lhA Comparing cards.
\lhN How do we proceed ?
\lhA Write a failing test.
\lhN Ok. Let compare a \spades 6 and a \hearts 6. These two cards should be considered equals in value.
\begin{lstlisting}[frame=single]
module Tests
where 
import Test.HUnit

main = runTestTT $ TestList 
       [compare "6h" "6s" ~?= EQ]
\end{lstlisting} % $
What is the result ?
\lhA \failure Failure: 
\begin{verbatim}
expected: EQ
 but got: LT
\end{verbatim}
We're comparing \il!String!s when we should compare \il!Card!s.
\lhN What is a \il!Card! ?
\lhA It's a new data type.
\lhN How do I create values of this type ?
\lhA Pretend you have a function from \il!String! to \il!Card!.
\lhN Ok. I'll just call that function \il!card! :
\begin{lstlisting}[frame=single]
main = runTestTT $ TestList 
       [compare (card "6h") (card "6s") ~?= EQ] 
\end{lstlisting} % $
What now ?
\lhA \error Error, as expected. Let me just write the function.
\begin{lstlisting}[frame=single]
module PokerHand
where

card :: String -> Card
\end{lstlisting}
\lhN \error This results in two errors: 
\begin{verbatim}
The type signature for `card' lacks an 
accompanying binding

Not in scope: type constructor or class 
`Card'
\end{verbatim}
What do we have to do ?
\lhA \error Create the \il!Card! type:
\begin{lstlisting}[frame=single]
data Card = C 

card :: String -> Card
\end{lstlisting}
Creating a single value, \il!C! is the simplest possible value we can write.
\lhN \error We still have one error: 
\begin{verbatim}
The type signature for `card' lacks an 
accompanying binding
\end{verbatim}
Can you implement the function ?
\lhA \error Here it is:
\begin{lstlisting}[frame=single]
data Card = C 

card :: String -> Card
card _ = C
\end{lstlisting}
Here the function is just producing the single value.
\lhN \error Woah! Here's a new one:
\begin{verbatim}
No instance for (Ord Card) arising 
from a use of `compare' 
Possible fix: add an instance 
declaration for (Ord Card)
\end{verbatim}
Can you fix this ?
\lhA \error Sure. Let's do what GHC suggests
\begin{lstlisting}[frame=single]
data Card = C deriving (Ord)

card :: String -> Card
card _ = C
\end{lstlisting}
\lhN \error Now we have this:
\begin{verbatim}
No instance for (Eq Card) arising 
from a use of `compare' 
Possible fix: add an instance 
declaration for (Eq Card)
\end{verbatim}
Can you fix it too ?
\lhA \error Again, let's do what GHC suggests 
\begin{lstlisting}[frame=single]
data Card = C deriving (Ord,Eq)

card :: String -> Card
card _ = C
\end{lstlisting}
\success Done!
\lhN Of course, this is just a \emph{fake}..
\lhA Then write another test.
\lhN Here you go:
\begin{lstlisting}[frame=single]
module Tests
where 
import Test.HUnit
import PokerHand

main = runTestTT $ TestList 
       [compare (card "6h") (card "6s") ~?= EQ
       ,compare (card "6h") (card "5s") ~?= GT]
\end{lstlisting} % $
How do we make it pass ?
\lhA \error What we should compare is the rank value of the card so let's store that value in the \il!Card! type:
\begin{lstlisting}[frame=single]
data Card = C Value deriving (Ord,Eq)
type Value = Int

card :: String -> Card
card _ = C 0
\end{lstlisting}
\failure Of course, the test now fails, as we must calculate the real value, not zero.. Let's think about it.
\lhN \failure Just make the test pass. I don't like to have to think on a red bar.
\lhA \failure Let's play ``\emph{fake it 'til you make it}'' then:
\begin{lstlisting}[frame=single]
card :: String -> Card
card ['6',_] = C 6
card ['5',_] = C 5 
\end{lstlisting}
\success Now it's obvious.
\lhN \success We just have to convert from \il!Char! to \il!Int!, using the \il!ord! function. Refactor it!
\lhA \success Ok!
\begin{lstlisting}[frame=single]
module PokerHand
where
import Char

data Card = C Value deriving (Ord,Eq)
type Value = Int

card :: String -> Card
card [c,_] = C $ (ord c) - (ord '0')
\end{lstlisting} % $
\success Done.
\lhN Done ? I think I have a new test to write. But first I'll do some refactoring, too.
\begin{lstlisting}[frame=single]
main = runTestTT $ TestList 
       [compare (card "6h") (card "6s") ~?= EQ
       ,compare (card "6h") (card "5s") ~?= GT]
\end{lstlisting} % $
You know about \il!comparing! right ?
\lhA Yes, and so does $GHCI$: \\

\il!comparing :: (Ord a) => (b -> a) -> b -> b -> Ordering! \\
\il! -- Defined in Data.Ord! \\


\il!comparing! takes a function from a type \il!b! to an ordered type \il!a!, two values of type \il!b! and gives the comparison using the given function.
\lhN Yes, so I can compare \il!String!s using the \il!card! function:
\begin{lstlisting}[frame=single]
module Tests
where 
import Test.HUnit
import PokerHand
import Data.Ord (comparing)

main = runTestTT $ TestList 
       [comparing card "6h" "6s" ~?= EQ
       ,comparing card "6h" "5s" ~?= GT]
\end{lstlisting} % $
\success What do you think ?
\lhA \success Nice!
\lhN Now for my new test:
\begin{lstlisting}[frame=single]
main = runTestTT $ TestList 
       [comparing card "6h" "6s" ~?= EQ
       ,comparing card "6h" "5s" ~?= GT
       ,comparing card "Th" "Js" ~?= LT]
\end{lstlisting} % $
\failure We're expecting \il!LT! but get \il!GT!. Can you make it pass?
\lhA
\failure I think so, by adding special cases:
\begin{lstlisting}[frame=single]
card :: String -> Card
card ['J',_] = C 11
card ['T',_] = C 10
card [c,_] = C $ (ord c) - (ord '0')
\end{lstlisting} % $
\success Done.
\lhN Good. Here's a new one.
\begin{lstlisting}[frame=single]
main = runTestTT $ TestList 
       [comparing card "6h" "6s" ~?= EQ
       ,comparing card "6h" "5s" ~?= GT
       ,comparing card "Th" "Js" ~?= LT
       ,comparing card "Kh" "Ah" ~?= LT]
\end{lstlisting} % $
\failure Go on.
\lhA \failure Ok. That's easy: give each card its value:
\begin{lstlisting}[frame=single]
card :: String -> Card
card ['A',_] = C 14
card ['K',_] = C 13
card ['J',_] = C 11
card ['T',_] = C 10
card [c,_] = C $ (ord c) - (ord '0')
\end{lstlisting} % $
\success Done.
\lhN We forgot the Queen value:
\begin{lstlisting}[frame=single]
main = runTestTT $ TestList 
       [comparing card "6h" "6s" ~?= EQ
       ,comparing card "6h" "5s" ~?= GT
       ,comparing card "Th" "Js" ~?= LT
       ,comparing card "Kh" "Ah" ~?= LT
       ,comparing card "Qh" "Kh" ~?= LT]
\end{lstlisting} % $
Can you add it ?
\lhA \failure Sure:
\begin{lstlisting}[frame=single]
card :: String -> Card
card ['A',_] = C 14
card ['K',_] = C 13
card ['Q',_] = C 12
card ['J',_] = C 11
card ['T',_] = C 10
card [c,_] = C $ (ord c) - (ord '0')
\end{lstlisting} % $
\success And we are done with card values.
\lhN We are, but I find the tests are a bit heavy. Can you think of a way to avoid repeating all these comparisons ?
\lhA Yes: we could test the sorting of a deck of card against an expected deck.
\lhN 
\begin{lstlisting}[frame=single]
module Tests
where 
import Test.HUnit
import PokerHand
import Data.List (sort)

deck = map card ["2h","9h","Th","Jh","Qh","Kh","Ah"]
main = runTestTT $ TestList 
       [sort deck  ~?= deck]
\end{lstlisting} % $
Is that what you mean ?
\lhA \error Yes, but we have a new problem.
\lhN \error Indeed:
\begin{verbatim}
No instance for (Show Card) arising
from a use of `~?='
Possible fix: add an instance 
declaration for (Show Card)
\end{verbatim}
Should we follow the suggestion ?
\lhA \error No. I don't think the \il!Card! type should derive the \il!Show! class just for testing reasons.
\lhN Then should we get back to the previous version of our tests ?
\lhA I have a better idea: instead of comparing lists of \il!Cards!, we can compare lists of \il!String!s.
\lhN \error You mean like this: 
\begin{lstlisting}[frame=single]
deck = ["2h","9h","Th","Jh","Qh","Kh","Ah"]
main = runTestTT $ TestList 
       [sort deck  ~?= deck]
\end{lstlisting} % $
\failure But now the test fail:
\begin{verbatim}
expected: ["2h","9h","Th","Jh","Qh","Kh","Ah"]
 but got: ["2h","9h","Ah","Jh","Kh","Qh","Th"]
\end{verbatim}
Do you see why ?
\lhA \failure Of course: we don't use the \il!Card! and the \il!card! function type any more!
\lhN You are right. Let's use it in the comparison:
\begin{lstlisting}[frame=single]
import Data.Ord (comparing)
import Data.List (sortBy)

deck = ["2h","9h","Th","Jh","Qh","Kh","Ah"]
main = runTestTT $ TestList 
       [sortBy (comparing card) deck  ~?= deck]
\end{lstlisting} % $
\success Now it works!
\lhA Yes!
\lhN I wonder what would the test show if it failed. Let's falsify the test:
\begin{lstlisting}[frame=single]
deck = ["9h","2h","Th","Jh","Qh","Kh","Ah"]
main = runTestTT $ TestList 
       [sortBy (comparing card) deck  ~?= deck]
\end{lstlisting} % $
\failure Don't change the code: what's the message ?
\lhA \failure Here it is:
\begin{verbatim}
expected: ["9h","2h","Th","Jh","Qh","Kh","Ah"]
 but got: ["2h","9h","Th","Jh","Qh","Kh","Ah"]
\end{verbatim}
The test properly outputs the results as a list of \il!String!s. We can get back to the correct version. Oh, and use \il!words!.
\lhN You are right.
\begin{lstlisting}[frame=single]
module Tests
where 
import Test.HUnit
import PokerHand
import Data.Ord (comparing)
import Data.List (sortBy)

deck = words "2h 9h Th Jh Qh Kh Ah"
main = runTestTT $ TestList 
       [sortBy (comparing card) deck  ~?= deck]
\end{lstlisting} % $
\success Are we done with comparing card values ?
\lhA Not yet.
\lhend